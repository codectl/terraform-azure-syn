module "naming" {
  source  = "cloudnationhq/naming/azure"
  version = "~> 0.26"

  suffix = ["demo", "dev"]
}

module "rg" {
  source  = "cloudnationhq/rg/azure"
  version = "~> 3.0"

  groups = {
    syn = {
      name     = module.naming.resource_group.name_unique
      location = "germanywestcentral"
    }
  }
}

module "storage" {
  source  = "cloudnationhq/sa/azure"
  version = "~> 5.0"

  storage = {
    name                = module.naming.storage_account.name_unique
    location            = module.rg.groups.syn.location
    resource_group_name = module.rg.groups.syn.name
    is_hns_enabled      = true

    file_systems = {
      adls-gen2 = {
        name = module.naming.storage_data_lake_gen2_filesystem.name
      }
    }
  }
}

module "kv" {
  source  = "cloudnationhq/kv/azure"
  version = "~> 6.0"

  vault = {
    name                     = module.naming.key_vault.name_unique
    location                 = module.rg.groups.syn.location
    resource_group_name      = module.rg.groups.syn.name
    purge_protection_enabled = true

    secrets = {
      random_string = {
        synapse-admin-password = {
          length      = 24
          special     = true
          min_special = 2
          min_upper   = 2
        }
      }
    }

    keys = {
      workspace-encryption-key = {
        key_type = "RSA"
        key_size = 2048
        key_opts = [
          "sign", "unwrapKey",
          "verify", "wrapKey"
        ]
      }
    }
  }
}

module "uai" {
  source  = "cloudnationhq/uai/azure"
  version = "~> 3.0"

  identity = {
    name                = module.naming.user_assigned_identity.name
    location            = module.rg.groups.syn.location
    resource_group_name = module.rg.groups.syn.name
  }
}

module "rbac" {
  source  = "cloudnationhq/rbac/azure"
  version = "~> 4.0"

  role_assignments = {
    synapse_uai = {
      object_id = module.uai.identity.principal_id
      type      = "ServicePrincipal"
      roles = {
        "Key Vault Crypto Service Encryption User" = {
          scopes = {
            kv = {
              id = module.kv.vault.id
            }
          }
        }
      }
    }
  }
}

module "synapse" {
  source  = "cloudnationhq/syn/azure"
  version = "~> 3.0"

  workspace = {
    name                                 = module.naming.synapse_workspace.name_unique
    storage_data_lake_gen2_filesystem_id = module.storage.file_systems.adls-gen2.id
    location                             = module.rg.groups.syn.location
    resource_group_name                  = module.rg.groups.syn.name
    sql_administrator_login              = "sqladminuser"
    sql_administrator_login_password     = module.kv.secrets.synapse-admin-password.value

    identity = {
      type         = "SystemAssigned, UserAssigned"
      identity_ids = [module.uai.identity.id]
    }

    customer_managed_key = {
      key_versionless_id        = module.kv.keys.workspace-encryption-key.versionless_id
      key_name                  = module.kv.keys.workspace-encryption-key.name
      user_assigned_identity_id = module.uai.identity.id
    }
  }
}
